; БИБЛИОТЕКА ПРЕРЫВНИЯ АППАРАТНОГО
;ИНИЦИАЛИЗАЦИИ CP/M И ОЧИСТОК ЭКРАНА



AR2	EQU 0BH
DOM	EQU 9
STR	EQU 10
LEFT	EQU 5
UP	EQU 6
RIGHT	EQU 7
DOWN	EQU 8
TAB	EQU 1
VK	EQU 3
ZB	EQU 4
PS	EQU 2
F1	EQU 12
F2	EQU 13
F3	EQU 14
F4	EQU 15
F5	EQU 16
SPACE	EQU 40H


PUSHA MACRO REG
	PUSH B
	PUSH D
	PUSH H
	IF NOT NUL REG
	PUSH PSW
	ENDIF
	ENDM
POPA MACRO REG
	IF NOT NUL REG
	POP PSW
	ENDIF
	POP H
	POP D
	POP B
	ENDM

;  ЗНАЧЕНИЯ:  MUSIC-ВСТАВИТЬ ПОДДЕРЖКУ МУЗЫКИ
;     NOKEY -  ВЫКЛЮЧИТЬ ПРЕОБРАЗОВАНИЕ КОДОВ GETKEY
APINIT:
	PUSHA 1
	LHLD TIMER ;ТАЙМЕРА
	INX H	   ;ХВАТАЕТ НА 21 МИНУТУ
	SHLD TIMER ;(65536/50)
	LXI H,PEREP
	XRA A
	CMP M
	JZ INIT2
	DCR M
	LXI D,100FH
	LXI H,COLR15
INIT1:
	MOV A,E
	OUT 2
	MOV A,M
	OUT 0CH
	OUT 0CH
	NOP
	NOP
	DCX H
	OUT 0CH
	DCR E
	DCR D
	OUT 0CH
	JNZ INIT1
INIT2:
	MVI A,8AH
	OUT 0	
	LXI H,KEYS
	MVI A,0FEH
INIT3:
	MOV B,A
	OUT 3
	IN 2
	MOV M,A
	MOV A,B
	INX H
	RLC
	JC INIT3
	CALL GETCODE
	MVI A,88H
	OUT 0
	LDA SCROLL
	OUT 3
	LDA BORDER
	OUT 2
	IN 7
	ANI 0F8H
	STA JOYPU
	LXI H,RUSCON
	IN 1
	RLC
	JC @NONAJ
	XRA A
	ORA M
	JZ @NAJ
	DCR M
	LDA KEYPAD
	XRI 2
	STA KEYPAD
	JMP @NAJ
@NONAJ:	MVI M,1
@NAJ:
	LDA KEYPAD
	ANI 2
	RLC
	RLC
	OUT 1

	POPA 1
	EI 
	RET
KEYS:	DS 8
KEY:	DB 0
KEYPAD:	DB 0
RUSCON:	DB 1
SCROLL:	DB 255
BORDER:	DB 0
OLD39H:	DW 0
COLR0:	DB 0,0,7,7,38H,38H,2BH,2BH,0C6H,0C6H
	DB 03,3,038H,038H,0ADH
COLR15:	DB 0ADH
PEREP:	DB 250
TIMER:	DS 2

GETCODE:
	XRA A
	STA KEY
	MVI C,0
SETCOD:	MVI B,8
	LXI H,KEYS
SETCO:	MOV A,M
	CPI 0FFH
	JNZ YKLAV
	MOV A,C
	ADI 8
	MOV C,A
	DCR B
	INX H
	JNZ SETCO
	RET
YKLAV:	RAR
	INR C
	JC YKLAV
	MOV A,C
	STA KEY
	RET

WAITKEY MACRO
	LDA KEY
	ORA A
	JZ $-4
	ENDM
LPAUSE	MACRO
;/// небольшая пауза (1/10 с) |||
	REPT 7
	HLT
	ENDM
	ENDM



TURBOCLS MACRO
	LOCAL TC1,TCEND
	JMP TCEND
@TC:
	DI
	LXI H,0
	DAD SP
	LXI SP,0E000H
	LXI D,0
	LXI B,0CFFH
	XRA A
TC1:	REPT 4
	PUSH D
	ENDM
	DCX B
	CMP B
	JNZ TC1
	SPHL
	EI
	RET
TCEND:
TURBOCLS MACRO
	CALL @TC
	ENDM
	TURBOCLS
	ENDM
