; MATH.INC 
;
LDAI	MACRO
;ЗАГРУЗКА С  ИНКРЕМЕНТОМ
	LDAX D
	INX D
	ENDM
LDAD	MACRO
;ЗАГРУЗКА С ДЕКРЕМЕНТОМ
	LDAX D
	DCX D
	ENDM
LOOP	MACRO WHERE
;ПЕТЛЯ ПО СЧЕТСЧИКУ В 	<BC>
	DCX B
	MOV A,B
	ORA C
	JNZ WHERE
	ENDM
STAI	MACRO
;ЗАПИСЬ С ИНКРЕМЕНТОМ
	STAX D
	INX D
	ENDM
NEG	MACRO ?REG
	IF NOT NUL ?REG
	MOV A,?REG
	ENDIF
	CMA
	INR A
	IF NOT NUL ?REG
	MOV ?REG,A
	ENDIF
	ENDM

LDIR MACRO FROM,TO,HOWM
	LOCAL LDIREND
	JMP LDIREND
mLDIR:
	PUSH PSW
	MOV A,M
	STAX D
	INX H
	INX D
	LOOP mLDIR+1
	POP PSW
	RET
LDIREND:
LDIR	MACRO ?FROM,?TO,SKOLKO
	IF NOT NUL ?FROM
	LXI H,?FROM
	ENDIF
	IF NOT NUL ?TO
	LXI D,?TO
	ENDIF
	IF NOT NUL SKOLKO
	LXI B,SKOLKO
	ENDIF
	CALL mLDIR
	ENDM
	LDIR FROM,TO,HOWM
	ENDM

ADDHL MACRO
	ADD L
	MOV L,A
	JNC $+4
	INR H
	ENDM
ADDDE MACRO
	ADD E
	MOV E,A
	JNC $+4
	INR D
	ENDM
SUBHB MACRO
	MOV A,L
	SUB C
	MOV L,A
	MOV A,H
	SBB B
	MOV H,A
	ENDM
SUBHD MACRO
	MOV A,L
	SUB E
	MOV L,A
	MOV A,H
	SBB D
	MOV H,A
	ENDM
CMPHD MACRO
	MOV A,H
	CMP D
	JNZ $+5
	MOV A,L
	CMP E
	ENDM

RND	MACRO REG
	LOCAL MASRND,pMASRND,@RNDEND
	JMP @RNDEND
@RND:	PUSH H
	PUSH B
	LXI H,MASRND
	LDA pMASRND
	MOV C,A
	ADI 13
	ANI 1FH
	STA pMASRND
	ADDHL
	MOV A,C
	MOV C,M
	LXI H,MASRND
	ADDHL
	MOV A,M
	ADD C
	ACI 1
	MOV M,A
	POP B
	POP H
	RET
pMASRND:
	DB 0
MASRND:
	DS 20H
@RNDEND:
RND	MACRO ?REG
	CALL @RND
	IF NOT NUL ?REG
	MOV ?REG,A
	ENDIF
	ENDM
	RND REG
	ENDM


ROL	MACRO REG
;SHIFT LEFT REG WITH CY
	MOV A,REG
	RAL
	MOV REG,A
	ENDM
ROR	MACRO REG
;SHIFT RIGHT REG  WITH CY
	MOV A,REG
	RAR
	MOV REG,A	
	ENDM
XLAT	MACRO REG
;ВЫБОРКА ИЗ ТАБЛИЦЫ [HL+A]
	ADDHL
	IF NUL REG
	MOV A,M
	ELSE
	MOV REG,M
	ENDIF
	ENDM
LODSB	MACRO REG
	LDAX D
	INX D
	IF NOT NUL REG
	MOV REG,A
	ENDIF
	ENDM
STOSB	MACRO REG
	IF NUL REG
	MOV M,A
	ELSE
	MOV M,REG
	ENDIF
	INX H
	ENDM
MOVSB	MACRO
;[DE]->[HL] С ИНКРЕМЕНТОМ
	LDAX D
	MOV M,A
	INX D
	INX H
	ENDM

BELL	MACRO
; АГАТОВСКИЙ CHR$(7)
	LOCAL BELLEND,BELL1
	JMP BELLEND
@BELL:
	PUSHA 1
	MVI H,0
	MOV C,H
	DI
BELL1:	MVI A,12
	CALL WAIT
	MOV A,H
	XRI 1
	MOV H,A
	OUT 1
	DCR C
	JNZ BELL1
	EI
	POPA 1
	RET
WAIT:
	STC
	PUSH PSW
	SBI 1
	JNZ $-2
	POP PSW
	SBI 1
	JNZ WAIT+1
	RET
BELLEND:
BELL	MACRO
	CALL @BELL
	ENDM
	BELL
	ENDM

