; MATH.INC 
;
LDAI	MACRO
;ЗАГРУЗКА С  ИНКРЕМЕНТОМ
	LDAX D
	INX D
	ENDM
LDAD	MACRO
;ЗАГРУЗКА С ДЕКРЕМЕНТОМ
	LDAX D
	DCX D
	ENDM
LOOP	MACRO WHERE
;ПЕТЛЯ ПО СЧЕТСЧИКУ В 	<BC>
	DCX B
	MOV A,B
	ORA C
	JNZ WHERE
	ENDM
STAI	MACRO
;ЗАПИСЬ С ИНКРЕМЕНТОМ
	STAX D
	INX D
	ENDM
NEG	MACRO ?REG
	IF NOT NUL ?REG
	MOV A,?REG
	ENDIF
	CMA
	INR A
	IF NOT NUL ?REG
	MOV ?REG,A
	ENDIF
	ENDM

LDIR MACRO FROM,TO,HOWMUCH
	LOCAL LDIREND
	JMP LDIREND
mLDIR:
	PUSH PSW
	MOV A,M
	STAX D
	INX H
	INX D
	LOOP mLDIR+1
	POP PSW
	RET
LDIREND:
LDIR	MACRO ?FROM,?TO,?HOWMUCH
	IF NOT NUL ?FROM
	LXI H,?FROM
	ENDIF
	IF NOT NUL ?TO
	LXI D,?TO
	ENDIF
	IF NOT NUL ?HOWMUCH
	LXI B,?HOWMUCH
	ENDIF
	CALL mLDIR
	ENDM
	LDIR FROM,TO,HOWMUCH	
	ENDM

XDIR MACRO FROM,TO,HOWMUCH
	LOCAL @XDIREND
	JMP @XDIREND
@XDIR:	PUSH PSW
	MOV A,M
	PUSH PSW
	  LDAX D
	  MOV M,A
	POP PSW
	STAX D
	INX H
	INX D
	LOOP @XDIR+1
	POP PSW
	RET
@XDIREND:	
XDIR	MACRO ?FROM,?TO,?HOWMUCH
	IF NOT NUL ?FROM
	LXI H,?FROM
	ENDIF
	IF NOT NUL ?TO
	LXI D,?TO
	ENDIF
	IF NOT NUL ?HOWMUCH
	LXI B,?HOWMUCH
	ENDIF
	CALL @XDIR
	ENDM
	XDIR FROM,TO,HOWMUCH
	ENDM
ADDHL MACRO
	ADD L
	MOV L,A
	JNC $+4
	INR H
	ENDM
ADDDE MACRO
	ADD E
	MOV E,A
	JNC $+4
	INR D
	ENDM
SUBHB MACRO
	MOV A,L
	SUB C
	MOV L,A
	MOV A,H
	SBB B
	MOV H,A
	ENDM
SUBHD MACRO
	MOV A,L
	SUB E
	MOV L,A
	MOV A,H
	SBB D
	MOV H,A
	ENDM
CMPHD MACRO
	MOV A,H
	CMP D
	JNZ $+5
	MOV A,L
	CMP E
	ENDM

RND	MACRO REG
	LOCAL MASRND,pMASRND,@RNDEND
	JMP @RNDEND
@RND:	PUSH H
	PUSH B
	LXI H,MASRND
	LDA pMASRND
	MOV C,A
	ADI 13
	ANI 1FH
	STA pMASRND
	ADDHL
	MOV A,C
	MOV C,M
	LXI H,MASRND
	ADDHL
	MOV A,M
	ADD C
	ACI 1
	MOV M,A
	POP B
	POP H
	RET
pMASRND:
	DB 0
MASRND:
	DS 20H
@RNDEND:
RND	MACRO ?REG
	CALL @RND
	IF NOT NUL ?REG
	MOV ?REG,A
	ENDIF
	ENDM
	RND REG
	ENDM


ROL	MACRO REG
;SHIFT LEFT REG WITH CY
	MOV A,REG
	RAL
	MOV REG,A
	ENDM
ROR	MACRO REG
;SHIFT RIGHT REG  WITH CY
	MOV A,REG
	RAR
	MOV REG,A	
	ENDM
XLAT	MACRO REG
;ВЫБОРКА ИЗ ТАБЛИЦЫ [HL+A]
	ADDHL
	IF NUL REG
	MOV A,M
	ELSE
	MOV REG,M
	ENDIF
	ENDM
LODSB	MACRO REG
	LDAX D
	INX D
	IF NOT NUL REG
	MOV REG,A
	ENDIF
	ENDM
STOSB	MACRO REG
	IF NUL REG
	MOV M,A
	ELSE
	MOV M,REG
	ENDIF
	INX H
	ENDM
MOVSB	MACRO
;[DE]->[HL] С ИНКРЕМЕНТОМ
	LDAX D
	MOV M,A
	INX D
	INX H
	ENDM


MULTIDIV MACRO
MULT:
	LOCAL MULT1
	LXI H,0 ;AHL = DE*A
	MVI B,8
MULT1:	DAD H
	RAL
	JNC $+6
	DAD D
	ACI 0
	DCR B
	JNZ MULT1
	RET

DIVAC:
;	WARNING!!!!!!!!
; IF C=0 - ETERNAL CYCLE
	MVI B,255 ;A=A/C
	INR B
	SUB C
	JNC $-2
	ADD C
	MOV C,A
	MOV A,B
	RET

MULTCD:
	LOCAL MCD0
	MVI B,0
	MVI E,9
MCD0:	ROR C
	DCR E
	RZ
	MOV A,B
	JNC $+4
	ADD D
	RAR
	MOV B,A
	JMP MCD0

DIVBCA:
	LOCAL DBCA0
	PUSH H
	CMA
	INR A
	MOV E,A
	MVI D,-1
	LXI H,0
	MVI A,17
DBCA0:	PUSH H
	DAD D
	JNC $+4
	XTHL
	POP H
	PUSH PSW
	ROL C
	ROL B
	ROL L
	ROL H
	POP PSW
	DCR A
	JNZ DBCA0
;
	ORA A
	MOV A,H
	RAR
	MOV D,A
	MOV A,L
	RAR
	MOV E,A
	POP H
	RET
	ENDM
