; ФАЙЛ 	MUSIC.LIB
; НАЧАЛ РАБОТАТЬ НАД ФАЙЛОМ ПОЛПЕРВОГО НОЧИ
;   11 ЯНВАРЯ 2000 ГОДА ОТ  РОЖДЕСТВА
;     ГОСПОДНЕГО. АМИНЬ!!

; MACRO FOR KILLING ALL CHANNELS
ALLSOUNDOFF MACRO
	CALL CHAN1OFF
	CALL CHAN2OFF
	CALL CHAN3OFF
	ENDM
STARTPLAY MACRO ADDR1,ADDR2,ADDR3
;ВКЛЮЧЕНИЕ КАНАЛОВ ПО УКАЗАН.АДРЕСАМ
	IF NOT NUL ADDR1
	LXI H,ADDR1
	CALL CHAN1ON
	ENDIF
	IF NOT NUL ADDR2
	LXI H,ADDR2
	CALL CHAN2ON
	ENDIF
	IF NOT NUL ADDR3
	LXI H,ADDR3
	CALL CHAN3ON
	ENDIF
	ENDM
STOPPLAY MACRO NUMCHAN
;ВЫКЛЮЧЕНИЕ КАНАЛОВ
;ФОРМАТ ВЫЗОВА:
;	STOPPLAY - ВЫКЛЮЧЕНИЕ ВСЕХ КАНАЛОВ
;	STOPPLAY <1,3> -ВЫКЛЮЧЕНИЕ 1 И 3 КАНАЛОВ
;	STOPPLAY 3     -ВЫКЛЮЧЕНИЕ 3 КАНАЛА
	IF NUL NUMCHAN
	CALL CHAN1OFF
	CALL CHAN2OFF
	CALL CHAN3OFF
	ELSE
	IRP CH,<NUMCHAN> ;ПОВТОРИТЬ ДЛЯ  ЗАДАННОГО КАНАЛА
	CALL CHAN&CH&OFF
	ENDM
	ENDIF
	ENDM
;

 
; А ТЕПЕРЬ ОСНОВНОЙ МАКРОС!
MUSIC MACRO
YCHAN1:	DB 0
YCHAN2:	DB 0
YCHAN3:	DB 0
YNOTE1:	DB 0  ; NOTE PLAYING OR FREE
YNOTE2:	DB 0  ; 1-FREE,128-PLAYING,255-PAUSE
YNOTE3:	DB 0  ; 0,129-254 IS RESERVED
ADDRNOTE1:
	DW 0  ; ADDRESS CURRENT NOTE
ADDRNOTE2:
	DW 0
ADDRNOTE3:
	DW 0
BEGNOTE1:
	DW 0   ; ADDRESS OF START MELODY
BEGNOTE2:
	DW 0
BEGNOTE3:
	DW 0

OCTAVE1:
	DB 0  ; OCTAVE ('0'-'6')
OCTAVE2:
	DB 0
OCTAVE3:
	DB 0
LENGTH1:
	DB 0   ; LENGTH ('1'-DB 255)
LENGTH2:
	DB 0
LENGTH3:
	DB 0
WAVE1:
	DB 0     ; WAVE OF NOTE
WAVE2:
	DB 0
WAVE3:
	DB 0
ABSNOTE1:
	DW 0  ; ABSOLUTE NOTE
ABSNOTE2:
	DW 0
ABSNOTE3:
	DW 0
; РЕЖИМЫ 0-ПРОСТОЙ ЗВУК, 1-ПЛАВАЮЩИЙ
; НОМЕРА 2-255 ЗАРЕЗЕРВИРОВАНЫ
; ПЛАНИРУЕТСЯ :2-ВОЗРАСТАНИЕ НОТЫ,3-ПАДЕНИЕ НОТЫ
;		4-ВЗРЫВ НОТЫ (gDw)

MODE1:	DB 0
MODE2:	DB 0
MODE3:	DB 0

;
; АБСОЛЮТНЫЕ ЗНАЧЕНИЯ НОТ:
;
DO:	DW 45872
	DB 98H,59H,0CBH,2CH,66H,16H,33H,0BH
	DW 1433,717
DODIEZ:	DW 43290
	DB 90H,54H,47H,2AH,24H,15H,92H,0AH
	DW 1353,677
RE:	DW 40872
	DB 0D1H,4FH,0E8H,27H,0F4H,13H,0FAH,9
	DW 1277,639
REDIEZ:	DW 38560
	DW 4B55H,25ABH,12D5H,96BH
	DW 1205,602
MI:	DW 36408
	DW 471CH,238DH,11C7H,8E3H
	DW 1137,569
FA:	DW 34364
	DW 431EH,218FH,10C7H,863H
	DW 1074,537
FADIEZ:	DW 32432
	DW 3F5AH,1FADH,0FD6H,7EBH
	DW 1014,507
SOL:	DW 30612
	DW 3BCCH,1DE5H,0EF3H,779H
	DW 957,472
SOLDIEZ:
	DW 28902
	DW 3870H,1C38H,0E1CH,70EH
	DW 904,452
LA:	DW 27273
	DW 3544H,1AA2H,0D51H,6A9H
	DW 852,426
SIBEMOL:
	DW 25729
	DW 3247H,1924H,0D22H,649H
	DW 804,402
SI:	DW 24311
	DW 2F75H,17BAH,0BDDH,5EFH
	DW 759,380

;АДРЕСА НОТ В ПАМЯТИ
NOTES:	DW LA,SI,DO,RE,MI,FA,SOL
	DW DODIEZ,REDIEZ,FADIEZ,SOLDIEZ,SIBEMOL

SETADDRNOTE: ;SET IN BC ADDRESS NOTE IN A
	SUI 'A'
	ADD A
	LXI B,NOTES
	MOV L,A
	MVI H,0
	DAD B
	MOV C,M
	INX H
	MOV B,M
	RET

;
; START OF MAIN PROGRAM
;
PLAY1CHAN:
	LDA YNOTE1
	ORA A
	JM PLAY1NOTE
	MVI A,36H
	OUT 8
	LHLD ADDRNOTE1
P1C0X:	MOV B,M
	INX H
	MOV A,M
	STA OCTAVE1
	INX H
	MOV A,M
	STA LENGTH1
	INX H
	SHLD ADDRNOTE1
	MVI A,128
	STA YNOTE1
	MVI A,6
	STA WAVE1
	MOV A,B
	CPI 24H   ; КНЦ МЛД1.../
	JZ CHAN1OFF
	CPI 'R'   ; ПОВТОР?
	JZ FIRSTMELO1
	CPI 'P'     ;ПАУЗА?
	JZ SET1PAUSE
	CPI 'M'
	JZ SET1MODE ;СМЕНА РЕЖИМА
	CPI 'X'
	JZ SET1DRUM ;БАРАБАНЫ
	CALL SETADDRNOTE
P1C0:	LDA OCTAVE1
P1C1:	CPI 30H
	JZ P1C2
	INX B
	INX B
	DCR A
	JMP P1C1

P1C2:	LDAX B
	MOV L,A
	INX B
	LDAX B
	MOV H,A
	SHLD ABSNOTE1
	XRA A
	JMP PLAY1NOTE

PLAY2CHAN:
	LDA YNOTE2
	ORA A
	JM PLAY2NOTE
	MVI A,76H  ; ВЫКЛ/ВКЛ 1-ЫЙ КАНАЛ
	OUT 8
	LHLD ADDRNOTE2
P2C0X:	MOV B,M
	INX H
	MOV A,M
	STA OCTAVE2
	INX H
	MOV A,M
	STA LENGTH2
	INX H
	SHLD ADDRNOTE2
	MVI A,128
	STA YNOTE2
	MVI A,6
	STA WAVE2
	MOV A,B
	CPI 'R'
	JZ FIRST2MELO
	CPI '$'
	JZ CHAN2OFF
	CPI 'P'
	JZ SET2PAUSE
	CPI 'M'
	JZ SET2MODE
	CALL SETADDRNOTE
P2C0:	LDA OCTAVE2
	CPI 30H
	JZ P2C2
	INX B
	INX B
	DCR A
	JMP P2C0+3
P2C2:	LDAX B
	MOV L,A
	INX B
	LDAX B
	MOV H,A
	SHLD ABSNOTE2
	XRA A
	JMP PLAY2NOTE

PLAY3CHAN:
	LDA YNOTE3
	ORA A
	JM PLAY3NOTE
	MVI A,0B6H  ; ВКЛ/ВЫКЛ 2-ОЙ КАНАЛ
	OUT 8
	LHLD ADDRNOTE3
P3C0X:	MOV B,M
	INX H
	MOV A,M
	STA OCTAVE3
	INX H
	MOV A,M
	STA LENGTH3
	INX H
	SHLD ADDRNOTE3
	MVI A,128
	STA YNOTE3
	MVI A,6
	STA WAVE3
	MOV A,B
	CPI 'R'
	JZ FIRST3MELO
	CPI '$'
	JZ CHAN3OFF
	CPI 'P'
	JZ SET3PAUSE
	CPI 'M'
	JZ SET3MODE
	CALL SETADDRNOTE
P3C0:	LDA OCTAVE3
	CPI 30H
	JZ P3C2
	INX B
	INX B
	DCR A
	JMP P3C0+3
P3C2:	LDAX B
	MOV L,A  ;LOW BYTE GOES FIRST
	INX B
	LDAX B
	MOV H,A  ;HIGH ONE GOES AFTER
	SHLD ABSNOTE3
	XRA A
	JMP PLAY3NOTE

;ОБРАБОТКА НОТ ЗАКОНЧИЛАСЬ, ТЕПЕРЬ НАЧАЛАСЬ/////
; САМА ИГРА НОТ ИЛИ ПАУЗА///////////////////////
PLAY1NOTE:
	CPI 129
	JZ PLAY1DRUM
	LDA WAVE1
	DCR A
	STA WAVE1
	MOV B,A
	LHLD ABSNOTE1
	LDA MODE1
	ORA A
	JZ P1C6
	MOV A,B
	LXI B,28H
	CPI 3
	JNC P1C5
	LXI B,-28H
P1C5:	DAD B
	SHLD ABSNOTE1
P1C6:	LDA YNOTE1
	INR A
	JZ @NO1PLAY
	MOV A,L
P1C7:	OUT 0BH ; SOUND
	MOV A,H
	OUT 0BH
@NO1PLAY:
	LXI H,WAVE1
	XRA A
	ORA M
	RNZ
	MVI M,6
	LDA LENGTH1
	DCR A
	STA LENGTH1
	SUI 30H
	RNZ
	STA YNOTE1 ; PLAY NEXT NOTE
	RET
PLAY2NOTE:
	LXI H,WAVE2
	DCR M
	MOV B,M
	LDA MODE2
	ORA A
	LHLD ABSNOTE2
	JZ P2C6
	MOV A,B
	LXI B,28H
	CPI 3
	JNC P2C5
	LXI B,-28H
P2C5:	DAD B
	SHLD ABSNOTE2
P2C6:	LDA YNOTE2
	INR A
	JZ @NO2PLAY
	MOV A,L
P2C7:	OUT 0AH
	MOV A,H
	OUT 0AH
@NO2PLAY:
	LXI H,WAVE2
	XRA A
	CMP M
	RNZ
	MVI M,6
	LXI H,LENGTH2
	DCR M
	MOV A,M
	SUI 30H
	RNZ
	STA YNOTE2
	RET

PLAY3NOTE:
	LXI H,WAVE3
	DCR M
	MOV B,M
	LHLD ABSNOTE3
	LDA MODE3
	ORA A
	JZ P3C6
	MOV A,B
	LXI B,28H
	CPI 3
	JNC P3C5
	LXI B,-28H
P3C5:	DAD B
	SHLD ABSNOTE3
P3C6:	LDA YNOTE3
	INR A
	JZ @NO3PLAY
	MOV A,L
P3C7:	OUT 9
	MOV A,H
	OUT 9
@NO3PLAY:
	LXI H,WAVE3
	XRA A
	ORA M
	RNZ
	MVI M,6
	LXI H,LENGTH3
	DCR M
	MOV A,M
	CPI 30H
	RNZ
	MVI A,1      ; НОТА ОСВОБОДИЛАСЬ!
	STA YNOTE3
	RET

; ОТКЛЮЧЕНИЕ  КАНАЛОВ
CHAN1OFF:
	XRA A
	STA YCHAN1
	MVI A,36H
	OUT 8
	RET
CHAN2OFF:
	XRA A
	STA YCHAN2
	MVI A,76H
	OUT 8
	RET
CHAN3OFF:
	XRA A
	STA YCHAN3
	MVI A,0B6H
	OUT 8
	RET

; НАЧАЛА МЕЛОДИЙ И ВКЛЮЧЕНИЕ КАНАЛОВ
FIRSTMELO1:
	LHLD BEGNOTE1
CHAN1ON:
	MVI A,1
	STA YCHAN1
	STA YNOTE1
	SHLD BEGNOTE1
	SHLD ADDRNOTE1
	RET

FIRST2MELO:
	LHLD BEGNOTE2
CHAN2ON:
	MVI A,1
	STA YCHAN2
	STA YNOTE2
	SHLD BEGNOTE2
	SHLD ADDRNOTE2
	RET

FIRST3MELO:
	LHLD BEGNOTE3
CHAN3ON:
	MVI A,1
	STA YCHAN3
	STA YNOTE3
	SHLD BEGNOTE3
	SHLD ADDRNOTE3
	RET
; УСТАНОВКА ПРИЗНАКА ПАУЗЫ ДЛЯ КАЖДОЙ НОТЫ
SET1PAUSE:
	MVI A,255
	STA YNOTE1
	JMP P1C2 ; ПРОДОЛЖЕНЕ ОБРАБОТКИ	
SET2PAUSE:
	MVI A,255
	STA YNOTE2
	JMP P2C2
SET3PAUSE:
	MVI A,-1
	STA YNOTE3
	JMP P3C2
SET1MODE:
	LDA OCTAVE1
	SUI 30H
	STA  MODE1
	DCX H
	JMP P1C0X
SET2MODE:
	LDA OCTAVE2
	SUI 30H
	STA  MODE2
	DCX H
	JMP P2C0X
SET3MODE:
	LDA OCTAVE3
	SUI 30H
	STA MODE3
	DCX H
	JMP P3C0X

;////БАРАБАНЫ,УДАРНИКИ,ТАРЕЛКИ\\\\\
SET1DRUM:
	MVI A,129
	STA YNOTE1
	LXI  H,1000H
	SHLD ABSNOTE1
PLAY1DRUM:
	LXI H,WAVE1
	DCR M
	LHLD ABSNOTE1
	INR H
	MOV A,L
	SUI 8
	MOV L,A
	SHLD ABSNOTE1
	JMP P1C7 ;ОСТАЛЬНОЕ ЗА НАС СДЕЛАЕТ ОСНОВНОЙ
		 ;БЛОК ИГРЫ  НОТЫ

; ЗАВЕРШЕНИЕ МАКРОСА MUSIC С
; ВОЗМОЖНОСТЬЮ ДАЛЬНЕЙШЕГ РАСШИРЕНИЯ ИЛИ 
; ПОЛНОЙ ПЕРЕРАБОТКИ!
	ENDM
